#version 460 core
layout (local_size_x = 8, local_size_y = 8, local_size_z = 4) in;
layout (rgba16f, binding = 0) writeonly uniform image3D diskVolume;

uniform float rMin;
uniform float rMax;
uniform float zMin;
uniform float zMax;
uniform float uTime;

const float PI = 3.14159265359;


float hash(vec3 p) {
    return fract(sin(dot(p, vec3(17.23, 53.11, 91.17))) * 43758.5453);
}

float valueNoise(vec3 p) {
    vec3 i = floor(p);
    vec3 f = fract(p);
    f = f * f * (3.0 - 2.0 * f);
    float n000 = hash(i + vec3(0,0,0));
    float n100 = hash(i + vec3(1,0,0));
    float n010 = hash(i + vec3(0,1,0));
    float n110 = hash(i + vec3(1,1,0));
    float n001 = hash(i + vec3(0,0,1));
    float n101 = hash(i + vec3(1,0,1));
    float n011 = hash(i + vec3(0,1,1));
    float n111 = hash(i + vec3(1,1,1));
    float n00 = mix(n000, n100, f.x);
    float n10 = mix(n010, n110, f.x);
    float n01 = mix(n001, n101, f.x);
    float n11 = mix(n011, n111, f.x);
    float n0 = mix(n00, n10, f.y);
    float n1 = mix(n01, n11, f.y);
    return mix(n0, n1, f.z);
}

float fbmRidged(vec3 p) {
    float sum = 0.0;
    float amp = 0.5;
    float freq = 1.0;
    for (int i = 0; i < 6; ++i) {
        float n = 1.0 - abs(valueNoise(p * freq) - 0.5) * 2.0;
        sum += n * amp;
        amp *= 0.5;
        freq *= 2.0;
    }
    return sum;
}


void main() {
    ivec3 gid  = ivec3(gl_GlobalInvocationID);
    ivec3 size = imageSize(diskVolume);
    if (any(greaterThanEqual(gid, size))) return;

    // Keep r,z at texel centers
    float ur = (float(gid.y) + 0.5) / float(size.y);
    float uz = (float(gid.z) + 0.5) / float(size.z);

    // Make theta periodic: last slice duplicates first slice
    int tx = gid.x;
    int txWrapped = (tx == size.x - 1) ? 0 : tx;
    float uTheta = float(txWrapped) / float(size.x - 1);   // [0..1], periodic
    float theta = uTheta * 2.0 * PI - PI;


    float r = mix(rMin, rMax, ur);
    float y = mix(zMin, zMax, uz);

    float H = abs(y);
    float halfThickness = (zMax - zMin) * 5.5;
    float verticalFalloff = exp(-pow(H / (halfThickness * 0.7), 2.0));

    float falloffIn  = smoothstep(rMin, rMin * 1.2, r);
    float falloffOut = 1.0 - smoothstep(rMax * 0.8, rMax, r);
    float baseDensity = falloffIn * falloffOut * verticalFalloff;

    vec3 warpCoords = vec3(r * cos(theta), y, r * sin(theta)) * 1.5;
    vec3 warp = vec3(
        fbmRidged(warpCoords + 13.7),
        fbmRidged(warpCoords + 24.2),
        fbmRidged(warpCoords + 19.1)
    );
    vec3 warped = warpCoords + (warp - 0.5) * 2.0 * 1.2;

    float tangentialStretch = 15.0;
    vec3 filamentCoords = warped * vec3(1.0, tangentialStretch, 1.0) * 1.8;
    float filaments = pow(fbmRidged(filamentCoords), 4.0);

    vec3 clumpCoords = warped * 0.5;
    float clumps = fbmRidged(clumpCoords);

    float combinedNoise = mix(filaments, clumps, 0.6);

    float density = baseDensity * combinedNoise * 3.0; 
    density = pow(density * 1.5, 3.0);                

    float temp = pow(rMin / (r + 1e-4), 2.5);
    temp = clamp(pow(temp, 0.8), 0.0, 1.0);

    imageStore(diskVolume, gid, vec4(density, temp, 0.0, 1.0));
}

